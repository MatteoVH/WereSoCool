import songs/std.socool 

{ f: 260, l: 1/3, g: 1, p: 0 }

overtones = {
    O[
    	(7/6, 1/5, 1/3, 0),
    	(9/8, -1/3, 1, 1),
      	(1/1, 1/4, 1, -1),
  	]
}

thing1 = {
    overtones
	| Sequence [
		9/5^1,
		9/4^1,
		3/1^1,
        10/3^1, 
        Tm 0, 
        5/2^1,
        5/3^1, 
        Tm 0,
        15/8^3/2, 
        Tm 0,
	] 
    | Seq [
        AsIs, 
        Tm 4/5 | Length 2/3,
    ]
    | Overlay [
        AsIs | PanM -1, 
        Tm 3/2 
        | Gain 1/4 
        | Seq [
            Tm 3/2, 
            Tm 5/4 | Length 1/2
        ] 
        | Length 1/2
    ]
    | Overlay [AsIs, Invert | Tm 2]
}

bass = {
    O[
    	(3/2, 1, 1, -1/2),
    	(3/2, 0, 1, 1/2),
    	(1/1, 1, 1, 1),
      	(1/1, 0, 1, -1),
  	]
    | Gain 3/4
    | Seq [
        Tm 1/2 | Length 4, 
        Tm 4/3, 
        Tm 5/4,
    ]    
    > FitLength thing1
}

up1 = {
    Seq [
        2/5^1, 3/5^1, 4/5^2,
        5/4^1, 5/3^1, 15/8^1, 15/7^1, 15/6^1,
        5/2^10
    ] | Length 3/4    
}

up2 = {
    Seq [
        3/7^1, 4/7^2, 5/7^1, 6/7^2,
        7/6^1, 7/5^1, 7/4^1, 7/3^1,
        3/1^10
    ]
    > FitLength up1
}

up3 = {
    Seq [
        9/8^1, 11/8^1, 13/8^1, 15/8^1,
        25/12^1, 8/3^1, 10/3^1, 4^1
    ]
    > FitLength up1
}

ups = {
    Seq [
        O[
            (1/1, 0, 1, 0),
            (1/1, 3, 1, 0),
            (1/2, 0, 1, 0),
        ]
        | Overlay [
            up1 | PanA -1,
            up2 | PanA 1,
            up3
        ] 
        | Seq [
            AsIs, Tm 3/1 | Length 1/4 | Gain 1/2 
        ]
    ]
}

chord = {
    O[
        (7/1, 1, 1/10, -1),
        (7/1, 0, 1/10, 1),
        (5/1, 1, 1, -1),
        (5/1, 0, 1, 1),
        (13/4, 1, 1, -1),
        (13/4, 0, 1, 1),
        (11/4, 0, 1, -1),
        (11/4, 1, 1, 1),
        (11/4, 0, 1, -1),
        (9/4, 1, 1, 1),
        (9/4, 0, 1, -1),
        (5/3, 1, 1, 1),
        (5/3, 0, 1, -1),
        (5/4, 1, 1, 1),
        (5/4, 0, 1, -1),
        (15/16, 1, 1, 1),
        (15/16, 0, 1, -1),
        (1/2, 1, 1, 1),
        (1/2, 0, 1, -1),
        (1/4, 1, 1, 1),
        (1/4, 0, 1, -1),
    ]
    | Gain 3/4
}

main = {
    Seq [
        Overlay [
            thing1,
            bass 
        ] 
        | Seq [AsIs, Reverse]
        | Repeat 2,
        ups,
        0^1/100,
        chord | Length 3 | ModulateBy [std.fade_out | Reverse, std.fade_out],
        0^1/100,
    ]
    | Repeat 2
}
	
